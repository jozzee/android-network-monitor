apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Network monitor with live data'
                    url = 'https://github.com/jozzee/android-network-monitor'
                    licenses {
                        license {
                            name = 'License'
                            url = 'https://github.com/jozzee/android-network-monitor/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'jozzee'
                            name = 'Jozzee Devaa'
                            email = 'sitthiphong.mun@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/jozzee/android-network-monitor.git'
                        developerConnection = 'scm:git:ssh://github.com/jozzee/android-network-monitor.git'
                        url = 'https://github.com/jozzee/android-network-monitor/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.gpgKeyId"]
ext["signing.password"] = rootProject.ext["signing.gpgPassphrase"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.gpgFile"]

signing {
    sign publishing.publications
}
